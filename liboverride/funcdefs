divert(-1)
changecom(`###')
define(fatal, `
  divert(0)dnl
  errprint(`$1')dnl
  m4exit(1)dnl
')

define(`file_header', `
#include <stdio.h>
#include <dlfcn.h>
ifdef(`$1_includes', `$1_includes')
')

define(`override_header', `
  void *handle = NULL;
  //This will turn the function proto into a function pointer declaration
  patsubst($1_proto, $1, `(*real_func)') = NULL;
  handle = dlopen("/lib/libc.so.6", RTLD_LAZY);

  if (handle == NULL) {
    fprintf(stderr, "Failed to dlopen libc\n");
    return -1;
  }
  real_func = dlsym(handle, "$1");
  ')


# Function prototypes
define(`malloc_proto',`void *malloc(size_t size)')
define(`bind_proto',`int bind(int s, const struct sockaddr *addr, socklen_t addrlen)')
define(`connect_proto',`int connect(int s, const struct sockaddr *name, socklen_t namelen)')
define(`open_proto',`int open(const char *path, int flags, ...)')
define(`open_call', `path, flags')

# Function includes
define(`bind_includes', `network_includes')
define(`connect_includes', `network_includes')
define(`network_includes', `
#include <sys/types.h>
#include <sys/socket.h>
')
define(`open_includes', `
#include <fcntl.h>
')
define(`malloc_includes', `
#include <stdlib.h>
')

define(`override', `
  ifdef(`$1_proto', , `fatal(`No such function "$1" known')')
  file_header($1)
  $1_proto {
    override_header($1)
    $2

    return real_func(
      ifdef(`$1_call', `$1_call', 
      //RETURNVARS $1_proto
      )
    );
  }
  ')
divert(0)dnl
